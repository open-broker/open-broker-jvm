plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.30"
    id "maven-publish"
}

group = 'org.openbroker'
version = '1.0-SNAPSHOT'
description = 'Library for Open Broker'

defaultTasks 'test'

ext {
    kotlinLanguageVersion = '1.3'

    defaultEncoding = 'UTF-8'

    ver = [
        'kotlin'     : '1.3.30',
        'springBoot' : '2.1.4.RELEASE',
        'cloudEvents': '76dfa0814f',
    ]

    servicePackage = 'org.openbroker'
    servicePackageSlashes = servicePackage.replace('.', '/')
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {

    implementation(
        platform("org.springframework.boot:spring-boot-dependencies:$ver.springBoot"),
        platform("org.jetbrains.kotlin:kotlin-bom:$ver.kotlin"),
        "org.jetbrains.kotlin:kotlin-stdlib-jdk8",
        "com.github.open-broker:cloud-events-jvm:$ver.cloudEvents",
        "com.fasterxml.jackson.core:jackson-core",
        "com.fasterxml.jackson.module:jackson-module-kotlin",
        "com.fasterxml.jackson.datatype:jackson-datatype-jsr310",
    )

    testImplementation(
        "org.junit.jupiter:junit-jupiter-api",
    )

    testRuntime(
        "org.junit.jupiter:junit-jupiter-engine",
    )
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        javaParameters = true
        jvmTarget = JavaVersion.VERSION_1_8
        freeCompilerArgs += ['-Xjsr305=strict', '-progressive']
        languageVersion = kotlinLanguageVersion
        apiVersion = kotlinLanguageVersion
    }
}

tasks.withType(JavaCompile).all {
    options.encoding = defaultEncoding
    options.compilerArgs = ['-parameters', '-Xlint:unchecked']
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

test {

    useJUnitPlatform()

    reports.junitXml.enabled false
    reports.html.enabled false

    failFast = System.getProperty('failFast') != null
    include servicePackageSlashes + '/**/*Test.*'

    testLogging {
        events 'skipped', 'failed'
        exceptionFormat = 'full'

        debug {
            events "started", "passed", "skipped", "failed"
        }

        info {
            events "passed", "skipped", "failed"
        }
    }
}
